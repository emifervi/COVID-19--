program MergeSort;

var int a[20], b[20], limit;

func void printArr();
var int i;
{
    for i = 0 to limit {
        print(a[i], " ");
    }
    print("\n");
}

func void merge(int low, int mid, int high);
var int l1, l2, i;
{
    l1 = low;
    l2 = mid + 1;
    i = low;
    
    while (l1 <= mid && l2 <= high) {
        if(a[l1] <= a[l2]) {
            b[i] = a[l1];
            l1 = l1 + 1;
        }
        else {
            b[i] = a[l2];
            l2 = l2 + 1;
        }
        i = i + 1;
    }

    while(l1 <= mid) {
        b[i] = a[l1];
        i = i + 1;
        l1 = l1 + 1;
    }

    while(l2 <= high) {
        b[i] = a[l2];
        i = i + 1;
        l2 = l2 + 1;
    }

    for i = low to (high + 1) {
        a[i] = b[i];
    }
}

func void sort(int low, int high);
var int mid;
{   
    if( low < high) {
        mid = (low + high) / 2;
        sort(low, mid);
        sort(mid + 1, high);
        merge(low, mid, high);
    }
}

main();
var int i;
{   
    limit = 21;
    while (limit > 20 || limit <= 0) {
        print("Size of array (<20): ");
        input(limit);
    }
    
    // Read array
    for i = 0 to limit {
        print("a[", i, "]: ");
        input(a[i]);
    }

    print("List before sorting \n");
    printArr();

    sort(0, limit - 1);

    print("List after sorting \n");
    printArr();
}